buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven { url = "https://repo.spongepowered.org/repository/maven-public/" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
    }
}

plugins {
    id 'io.franzbecker.gradle-lombok' version '1.11'
    id 'java'
}

apply plugin: 'net.minecraftforge.gradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

repositories {
    maven { url = "https://jitpack.io" }
    maven { url = 'http://oss.sonatype.org/content/repositories/public/' }
}

configurations {
    embed
    implementation.extendsFrom(embed)
}

// Minecraft, MCP, Forge, and Java versions
sourceCompatibility = targetCompatibility = "1.8"
ext.mcversion = "1.12.2"
ext.forgeversion = "14.23.4.2707"

// Mod version
version = "3.0.9"
boolean isBeta = false
group = "org.dimdev.dimdoors"
archivesBaseName = "DimensionalDoors"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

// Build number
String fullVersion = ext.mcversion + "-" + version
if (System.getenv("TRAVIS_BUILD_NUMBER") != null) {
    fullVersion += "+${System.getenv("TRAVIS_BUILD_NUMBER")}"
} else {
    fullVersion += "+UNOFFICIAL"
}
if (isBeta) {
    fullVersion += "-b"
}
String jarVersion = fullVersion.replace("+", "-") // Github/Travis doesn't seem to support + in filenames

// Configuration
minecraft {
	mappings channel: 'snapshot', version: '20180703-1.12'
    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
        }

        server {

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
        }
    }
}

dependencies {
	embed 'com.flowpowered:flow-math:1.0.3'
    embed 'org.jgrapht:jgrapht-core:1.1.0'
    embed 'com.github.DimensionalDevelopment:poly2tri.java:master-SNAPSHOT'
    compileOnly 'com.github.DimensionalDevelopment:AnnotatedNBT:-SNAPSHOT'
    compileOnly 'io.github.opencubicchunks:cubicchunks:1.12.2-0.0.819.0-SNAPSHOT'
	minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2860'
}

// Tasks
compileJava {
    //options.compilerArgs += "-proc:only" // To debug AnnotatedNBT code generation
}

jar {
    archiveName = archivesBaseName + "-" + jarVersion + ".jar"
    from(configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude "LICENSE.txt", "META-INF/MANIFSET.MF", "META-INF/maven/**", "META-INF/*.RSA", "META-INF/*.SF"
    }
}

jar.finalizedBy('reobfJar') 

processResources {
    // This will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", "1.12.2"

    // Replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': "1.12.2"
    }

    // Copy everything else except mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task generatePocketSchematics(dependsOn: jar, type: JavaExec, group: "dimdoors") {
    classpath = files('build/libs/' + jar.archiveName)
    classpath += sourceSets.main.runtimeClasspath
    main = "org.dimdev.dimdoors.shared.tools.SchematicGenerator"
    args = ["src/main/resources/assets/dimdoors/pockets/schematic"]
}
