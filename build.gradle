buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id 'io.franzbecker.gradle-lombok' version '1.11'
    id 'java'
}

apply plugin: 'net.minecraftforge.gradle.forge'
//Only edit below this line, the above code adds and enables the nessasary things for Forge to be setup.

ext.modversion = "3.0.0-b1"
ext.mcversion = "1.12.2"
ext.forgeversion = "14.23.1.2555"
//spongeSchematicVersion = "1"

version = mcversion + "-" + modversion
group = "org.dimdev.dimdoors" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "dimdoors"
jar.archiveName = "dimdoors.jar" // Constant name for travis

sourceCompatibility = targetCompatibility = "1.8" // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
    version = "1.12.2-14.23.1.2555"
    runDir = "run"
    mappings = "snapshot_20171007"
    replace '${version}', project.version
    //replace '${spongeSchematicVersion}', spongeSchematicVersion
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

configurations {
    embed 
    compile.extendsFrom(embed)
}

dependencies {
    embed 'com.flowpowered:flow-math:1.0.3'
}

jar {
    from configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task generatePocketSchematics(dependsOn: jar, type: JavaExec, group: "dimdoors") {
    classpath = files('build/libs/dimdoors.jar')
    classpath += sourceSets.main.runtimeClasspath
    main = "org.dimdev.dimdoors.shared.tools.PocketSchematicGenerator"
    //noinspection GroovyAssignabilityCheck (IntelliJ is wrong)
    args "src/main/resources/assets/dimdoors/pockets/schematic"
}
